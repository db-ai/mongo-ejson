grammar ExtendedJSON
  root                   <- @__ (object / array) @__
                          %make_root

  object                 <- (non_empty_object / empty_object)

  pair                   <- @__ string @assignment value
                          %make_pair

  non_empty_object       <- @object_open
                              pair (@delimiter pair)*
                            @object_close
                          %make_object

  empty_object           <- @object_open
                            @object_close
                          %make_empty_object

  array                  <- non_empty_array / empty_array

  non_empty_array        <- @array_open
                              value (@delimiter value)*
                            @array_close
                          %make_array

  empty_array            <- @array_open
                            @array_close
                          %make_empty_array

  value                  <- @__ (json_values / mongo_literals / mongo_types) @__

  json_values            <- object / array / number / string / boolean / null
  mongo_literals         <- min_key / max_key / undefined / regexp_string
  mongo_types            <- object_id / bin_data / timestamp / number_long /
                            number_decimal / date / db_ref_type

  # Values
  boolean                <- (true / false)

  true                   <- "true"
                          %make_true

  false                  <- "false"
                          %make_false

  null                   <- "null"
                          %make_null

  string                 <- double_quote_string / single_quote_string

  double_quote_string    <- double_quote
                              ("\\" . / [^"])*
                            double_quote
                          %make_string

  single_quote_string    <- single_quote
                              ("\\" . / [^'])*
                            single_quote
                          %make_string

  hex_string             <- hex_single_quote / hex_double_quote
  hex_value              <- [a-fA-F0-9]+

  hex_single_quote       <- @single_quote
                              hex_value
                            @single_quote

  hex_double_quote       <- @double_quote
                              hex_value
                            @double_quote

  regexp_string          <- @slash
                              ("\\" . / [^\/])*
                            @slash
                            regexp_options?
                          %make_regexp

  regexp_options         <- [gims]+

  base64_string          <- base64_single_quote / base64_dobule_quote
  base64_value           <- [a-zA-Z0-9+\/]+ ("=" / "==")?

  base64_single_quote    <- @single_quote
                              base64_value
                            @single_quote

  base64_dobule_quote    <- @double_quote
                              base64_value
                            @double_quote

  number                 <- "-"? integer fraction? exponent?
                          %make_number

  integer                <- ("0" / [1-9] [0-9]*)
  fraction               <- ("." [0-9]+)
  exponent               <- (("e" / "E") ("+" / "-" / "") [0-9]+)

  integer_number         <- integer @""
                          %make_number

  number_string          <- number_string_single / number_string_double

  number_string_single   <- @single_quote
                              number
                            @single_quote

  number_string_double   <- @double_quote
                              number
                            @double_quote

  # Mongo Extensions

  min_key                <- "MinKey"
                          %make_min_key

  max_key                <- "MaxKey"
                          %make_max_key

  undefined              <- "undefined"
                          %make_undefined

  object_id              <- @"ObjectId"
                            @type_open
                              hex_string
                            @type_close
                          %make_object_id

  bin_data_type          <- value:(string / integer_number) _:""
  bin_data               <- @"BinData"
                            @type_open
                              bin_data_type
                            @delimiter
                              base64_string
                            @type_close
                          %make_bin_data

  timestamp_value        <- value:integer_number _:""
  timestamp              <- @"Timestamp"
                            @type_open
                              timestamp_value
                            @delimiter
                              timestamp_value
                            @type_close
                          %make_timestamp

  number_long_value      <- value:(number / number_string) _:""
  number_long            <- @"NumberLong"
                            @type_open
                              number_long_value
                            @type_close
                          %make_number_long


  number_decimal_value   <- value:(number / number_string) _:""
  number_decimal         <- @"NumberDecimal"
                            @type_open
                              number_decimal_value
                            @type_close
                          %make_number_decimal

  date_type              <- ("new" space+ "Date" / "ISODate")
  date_value             <- value:(integer_number / string) _:""
  date                   <- @date_type
                            @type_open
                              date_value
                            @type_close
                          %make_date

  db_ref_type            <- @"DBRef"
                            @type_open
                              string
                            @delimiter
                              string
                            @type_close
                          %make_db_ref

  single_quote           <- "'"
  double_quote           <- '"'
  left_paren             <- '('
  right_paren            <- ')'
  left_brace             <- '{'
  right_brace            <- '}'
  left_bracket           <- '['
  right_bracket          <- ']'
  comma                  <- ','
  colon                  <- ':'
  slash                  <- '/'
  space                  <- [\s]
  new_line               <- [\n]

  comment                <- "//" [^\n]*
  __                     <- (space / new_line / comment)*

  delimiter              <- @__ comma @__
  assignment             <- @__ @colon @__

  object_open            <- @__ @left_brace @__
  object_close           <- @__ @right_brace @__
  array_open             <- @__ @left_bracket @__
  array_close            <- @__ @right_bracket @__

  type_open              <- @space* @left_paren @space*
  type_close             <- @space* @right_paren
